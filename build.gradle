buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        // Note that the latest pre-release versions of Kotlin might not work in TravisCI
        // because TravisCI only searches for dependencies in central maven repository and ignores all other repositories.
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.4-2"
    }
}

apply plugin: "kotlin"
kotlin {
    experimental {
        coroutines "enable"
    }
}
repositories {
    jcenter()
    mavenCentral()
}

group = "org.rosettacode"
version = "0.0-SNAPSHOT"
sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.4"
    compile "com.thoughtworks.xstream:xstream:1.4.9"
    compile "org.http4k:http4k-core:2.24.0"
    compile "org.http4k:http4k-server-jetty:2.24.0"
    compile "org.http4k:http4k-client-apache:2.24.0"
    testCompile "io.kotlintest:kotlintest:1.3.7"

    // For Break_OO_privacy.kt and Reflection-List_properties.kt
    compile "org.jetbrains.kotlin:kotlin-reflect:1.1.4"
    // For Active_Directory-Connect.kt
    compile "org.apache.directory.api:api-all:1.0.0"
    // For Bernoulli_numbers.kt
    compile "org.apache.commons:commons-math3:3.3"
    // For RIPEMD-160.kt
    compile "org.bouncycastle:bcprov-jdk15on:1.55"
}

sourceSets {
    main.java.srcDirs += "src"
    test.java.srcDirs += "test"
}

task pull(type: JavaExec, dependsOn: "classes") {
    main = "scripts.PullFromRosettaCodeKt"
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.properties
}

task push(type: JavaExec, dependsOn: "classes") {
    main = "scripts.PushToRosettaCodeKt"
    classpath = sourceSets.main.runtimeClasspath
}

defaultTasks "classes"
